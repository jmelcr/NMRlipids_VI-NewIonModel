#set term postscript enhanced color  
set terminal postscript enhanced color dashed lw 1 "Helvetica" 22 
set output "../bound-CAs_conc-eccl17.eps"
#set size 3.07, 1.2
#set key above

ls_exp=10
ls_l14=1
ls_eccl17=2

# no. lipid in simulation
n_lipids=128

# columns:
# 1 nominal conc
# 2 no. bound cations
# 3 tot. no. cations in simulation
# 4 bound oxygens_POPC per cation
datafile="../../Data/POPC/CaCl/lipid14/bound_Ca_non-tail_q-sc_0.80_sig-sc_0.875_OPC3.dat"
# simulaiton: no NaCl, 313K

set style line ls_exp    pt 13 ps 2 lw 4 lc rgb '#000000' 
set style line ls_l14    pt  6 ps 2 lw 4 lc rgb '#e04010' dashtype 3
set style line ls_eccl17 pt  6 ps 2 lw 4 lc rgb '#DE3322' dashtype 3

#set key font ",13"
#set key at screen 2.10,screen 1.1 reverse Left # maxcols 2 maxrows 9 
#

# equation 18 from Altenbach and Seelig (Biochemistry 1984, 23, 3913-3920)
# assumes formation of a ternary complex -- 2 POPC molecules per 1 Calcium
# x: conc. bound cations / conc. POPC
# y: sqrt( conc_bound_ions / conc_solution_ions )
f(x) = ksqrt       * (1-2*x)
e(x) = sqrt(k_exp) * (1-2*x)
ksqrt  = 2.91878
k_exp  = 7.0  # no NaCl, 313K

fit f(x) datafile using ($2/n_lipids):(sqrt(1000*$2/n_lipids/($4)))  via ksqrt

set xrange [0:0.52]
set yrange [0:3.0]
set xtics format "%G"
set ytics format "%G"
set xlabel "C_b"
#set xlabel "[Ca^{2+}_{bound}/POPC]"
#set xlabel "number of Ca^{2+}_{bound} / POPC"
set ylabel "( C_b / C_I )^{1/2}"
#set ylabel "( [Ca^{2+}_{bound}/POPC] / [Ca^{2+}_{binding plane}] )^{1/2}"
#set ylabel "( Ca^{2+}_{bound} / Ca^{2+}_{free} )^{1/2}"
#set size 0.8, 0.43
plot \
     f(x) title 'fit of ternary complex binding model to simulation' ls ls_eccl17, \
     datafile using ($2/n_lipids):(sqrt(1000*$2/n_lipids/($4))) with points title 'ECC-POPC (simulations)' ls ls_eccl17 ,\
     e(x) title 'ternary complex binding model (experiment)' ls ls_exp
     #datafile using ($2/n_lipids):(sqrt($2/n_lipids/($4))) with points title 'ECC-lipids' ls ls_l14 ,\
     

